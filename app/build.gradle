plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.ddairy.eyebrows"
        minSdkVersion 29
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // Android apps need to be digitally signed with a certificate before installing.
    signingConfigs {
        // TODO: Look into before release.


        // Digitally signed for debug phones.
        // We use a bundled debug keystore, to allow debug builds from CI to be upgradable.
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        // TODO: Look into before release.
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        // A list of build features that can be disabled or enabled in an Android project.
        // We only need Compose to be enabled to work with android studio.
        aidl false
        buildConfig false
        compose true
        prefab = false
        renderScript = false
        resValues = false
        shaders = false
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_version"
    }
}

dependencies {

    // ----------------- implementation -----------------
    /* Dependencies used in release.
     */

    // Import the BoM for the Firebase platform.
    // Add the dependencies for any other desired Firebase products.
    // https://firebase.google.com/docs/android/setup#available-libraries
    implementation platform('com.google.firebase:firebase-bom:29.0.3')
    // Declare the dependency for the Firebase SDK for Google Analytics.
    // When using the BoM, you don't specify versions in Firebase library dependencies.
    implementation 'com.google.firebase:firebase-analytics-ktx'
    // Does not come with the bom above.
    // TODO: How to get started: https://firebase.google.com/docs/admob/android/quick-start - this can only be done once app is on the play store. I have already completed step 1a in the tutorial.
    // TODO: Uncomment: implementation 'com.google.android.gms:play-services-ads:20.5.0'

    // Jackson, used to deserialize objects to strings.
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    // Required to use the material theme in the theme.xml file.
    implementation "com.google.android.material:material:1.6.0-alpha01"

    // Kotlin Standard Library JDK 8 extension.
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // New package for splash screens.
    implementation "androidx.core:core-splashscreen:1.0.0-alpha02"

    // Compose libraries.
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    // Compose integration with Activity.
    implementation "androidx.activity:activity-compose:1.4.0"

    // Compose integration with Navigation.
    implementation "androidx.navigation:navigation-compose:2.4.0-rc01"

    // Accompanist is a group of libraries that aim to supplement Jetpack Compose with features that are commonly required by developers but not yet available.
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"

    // ----------------- androidTestImplementation -----------------
    /* adds dependency for androidTest source set
     * For android UI test. It needs virtual or real device (Espresso).
     * TODO: when writing these tests compare the dependencies to whats in the testing code lab.
     */

    // Android Core-Testing.
    // Helper for other arch dependencies, including JUnit test rules that can be used with LiveData.
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    // Espresso dependencies.
    // The AndroidX Test Library provides an extensive framework for testing Android apps.
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"

    // Assertions.
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    // Coroutines support libraries for Kotlin.
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'

    // Compose libraries.
    androidTestImplementation "androidx.compose.ui:ui-test:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    // ----------------- testImplementation -----------------
    /* Adds dependency for test source set.
     * For android independent test.
     */

    // JUnit 4 support.
    testImplementation 'junit:junit:4.13.2'
}

apply plugin: 'com.google.gms.google-services'